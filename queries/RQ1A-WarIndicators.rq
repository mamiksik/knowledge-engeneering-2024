#RQ1: Most important factor for SAP500 -> Influence of war news on stock prices

PREFIX ex: <http://kea.local/>

SELECT
    ?date
    (COUNT(DISTINCT ?warNews) as ?totalNews)
    (SUM(if(?sentiment = 'increase-support', 1, 0)) as ?increaseSupport)
    (SUM(if(?sentiment = 'decrease-support', 1, 0)) as ?decreaseSupport)
    (SUM(if(?sentiment = 'not-known', 1, 0)) as ?notKnown)
    (AVG(?priceBefore) as ?avgPriceBefore)
    (AVG(?priceAfter) as ?avgPriceAfter)
    ((?avgPriceAfter - ?avgPriceBefore) as ?avgPriceChangeAfter)
WHERE {
    # Select all the dates
    ?date a ex:Date.
    {
        # For each date, get the sentiment of the war news
        ?warNews a ex:WarNews ;
            ex:onDay ?date ;
            ex:sentiment ?sentiment .
    } UNION {
        # For each date, get the closing price of the stock
        ?pricePoint a ex:PricePoint ;
            ex:onDay ?date ;
            ex:hasClosingPrice ?price .
    } UNION {
        # Price the day before
        ?dateBefore a ex:Date ;
            ex:nextDay ?date .
        ?pricePointBefore a ex:PricePoint ;
            ex:onDay ?dateBefore ;
            ex:hasClosingPrice ?priceBefore .
    } UNION {
        # Price the day after
        ?dateAfter a ex:Date ;
            ex:previousDay ?date .
        ?pricePointAfter a ex:PricePoint ;
            ex:onDay ?dateAfter ;
            ex:hasClosingPrice ?priceAfter .
    }
}
GROUP BY ?date
HAVING (?avgPriceBefore > 0 && ?avgPriceAfter > 0)
ORDER BY ?date